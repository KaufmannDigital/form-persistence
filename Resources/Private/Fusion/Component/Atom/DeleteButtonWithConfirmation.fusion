prototype(PunktDe.Form.Persistence:Component.Atom.DeleteButtonWithConfirmation) < prototype(Neos.Fusion:Component) {
    action = ''
    index = 0
    fromObject = null
    fieldName = ''

    renderer = afx`
        <button class="neos-button neos-button-danger" title={I18n.translate('PunktDe.Form.Persistence:Modules.FormPreview:entry.delete.tooltip')} data-toggle="modal" href={'#user-' + props.index} data-neos-toggle="tooltip">
            <i class="fas fa-trash-alt icon-white"></i>
        </button>
        <div class="neos-hide" id={'user-' + props.index}>
            <div class="neos-modal-centered">
                <div class="neos-modal-content">
                    <div class="neos-modal-header">
                        <button type="button" class="neos-close neos-button" data-dismiss="modal"></button>
                        <div class="neos-header">
                            {I18n.translate('PunktDe.Form.Persistence:Modules.FormPreview:entry.delete.modal.title')}
                        </div>
                        <div>
                            <div class="neos-subheader">
                                <p>
                                    {I18n.translate('PunktDe.Form.Persistence:Modules.FormPreview:entry.delete.modal.body')}
                                </p>
                            </div>
                        </div>
                    </div>
                    <div class="neos-modal-footer">
                        <a href="#" class="neos-button" data-dismiss="modal">
                            {I18n.translate('PunktDe.Form.Persistence:Modules.FormPreview:entry.delete.modal.cancel')}
                        </a>
                        <Neos.Fusion.Form:Form attributes.style="display: inline-block" form.target.action={props.action} form.target.controller="Backend\\FormData" form.data.formDataEntry={props.fromObject}>
                            <Neos.Fusion.Form:Hidden field.name={props.fieldName}/>
                            <Neos.Fusion.Form:Button type="submit" attributes.class="neos-button neos-button-danger">
                                {I18n.translate('PunktDe.Form.Persistence:Modules.FormPreview:entry.delete.modal.confirm')}
                            </Neos.Fusion.Form:Button>
                        </Neos.Fusion.Form:Form>
                    </div>
                </div>
            </div>
            <div class="neos-modal-backdrop neos-in"></div>
        </div>
    `
}
