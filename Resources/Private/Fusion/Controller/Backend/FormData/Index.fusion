prototype(PunktDe.Form.Persistence:Backend.FormData.Index) < prototype(Neos.Fusion:Component) {

     exportDefinitionsLink = Neos.Fusion:UriBuilder {
        package = 'PunktDe.Form.Persistence'
        controller = 'Backend\\ExportDefinition'
        action = 'index'
        additionalParams {
            formIdentifier = ${formType.formIdentifier}
            hash = ${formType.hash}
        }
    }

    tableRows = Neos.Fusion:Loop {
        items = ${formTypes}
        itemName = 'formType'
        itemRenderer = Neos.Fusion:Component {

            date = ${Date.format(formType.date, 'Y-m-d H:i:s')}
            formDataEntry = ${formType}
            entryCount = ${formType.entryCount}

            options = Neos.Fusion:Loop {
                items = ${PunktDe.FormExport.ExportDefinition.getSuitableExportDefinitions(formType)}
                itemRenderer = Neos.Fusion.Form:Select.Option {
                    option.value = ${item.identifier}
                    content = ${item.label}
                }
            }

            renderer = afx`
                <tr>
                    <td>{props.formDataEntry.formIdentifier}</td>
                    <td>{String.crop(props.formDataEntry.hash,10)}</td>
                    <td>{props.entryCount}</td>
                    <td>{props.date}</td>
                    <td class="neos-action">
                        <Neos.Fusion.Form:Form form.target.action="download" form.target.controller="Backend\\FormData">
                            <Neos.Fusion.Form:Select field.name="exportDefinitionIdentifier">{props.options}</Neos.Fusion.Form:Select>
                            <Neos.Fusion.Form:Hidden field.name="formIdentifier" attributes.value={props.formDataEntry.formIdentifier}/>
                            <Neos.Fusion.Form:Hidden field.name="hash" attributes.value={props.formDataEntry.hash}/>
                            <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-primary">Download</Neos.Fusion.Form:Button>
                        </Neos.Fusion.Form:Form>
                    </td>
                    <td>
                        <Neos.Fusion.Form:Form form.target.action="preview" form.target.controller="Backend\\FormData" form.data.formDataEntry={props.formDataEntry}>
                            <Neos.Fusion.Form:Hidden field.name="formDataEntry[hash]"/>
                            <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-primary"><i class="fas fa-eye icon-white"></i></Neos.Fusion.Form:Button>
                        </Neos.Fusion.Form:Form>
                        <Neos.Fusion.Form:Form form.target.action="delete" form.target.controller="Backend\\FormData" form.data.formDataEntry={props.formDataEntry}>
                            <Neos.Fusion.Form:Hidden field.name="formDataEntry[hash]"/>
                            <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-danger"><i class="fas fa-trash-alt icon-white"></i></Neos.Fusion.Form:Button>
                        </Neos.Fusion.Form:Form>
                    </td>
                </tr>
            `
        }
    }

    renderer = afx`
        <div class="neos-content neos-container-fluid">
            <div class="neos-module-container">
                <table class="neos-table">
                    <thead>
                        <tr>
                            <th>
                                Form Identifier
                            </th>
                            <th>
                                Form Version
                            </th>
                            <th>
                                Entry Count
                            </th>
                            <th>
                                Latest Entry
                            </th>
                            <th/>
                            <th/>
                        </tr>
                    </thead>
                    <tbody>
                        {props.tableRows}
                    </tbody>
                </table>
            </div>
            <div class="neos-footer">
                <a class="neos-button neos-button-primary" title="Export definitions" href={props.exportDefinitionsLink}>Manage Export Definitions</a>
            </div>
        </div>
    `
}
