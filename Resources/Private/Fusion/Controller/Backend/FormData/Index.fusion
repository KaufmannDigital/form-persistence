prototype(PunktDe.Form.Persistence:Backend.FormData.Index) < prototype(Neos.Fusion:Component) {

     exportDefinitionsLink = Neos.Fusion:UriBuilder {
        package = 'PunktDe.Form.Persistence'
        controller = 'Backend\\ExportDefinition'
        action = 'index'
        additionalParams {
            formIdentifier = ${formType.formIdentifier}
            hash = ${formType.hash}
        }
    }

    tableRows = Neos.Fusion:Loop {
        items = ${formTypes}
        itemName = 'formType'
        itemRenderer = Neos.Fusion:Component {

            @context.formData = ${formType[0]}
            date = ${formType.latestDate}
            entryCount = ${formType.entryCount}

            options = Neos.Fusion:Loop {
                items = ${PunktDe.FormExport.ExportDefinition.getSuitableExportDefinitions(formData)}
                itemRenderer = Neos.Fusion.Form:Select.Option {
                    option.value = ${item.identifier}
                    content = ${item.label}
                }
            }

            deleteButton = PunktDe.Form.Persistence:Component.Atom.DeleteButtonWithConfirmation {
                action = 'delete'
                index = ${iterator.index}
                fromObject = ${formData}
                fieldName = 'formDataEntry[hash]'
            }

            contentDimensions = Neos.Fusion:Map {
                items = ${formData.contentDimensions}
                itemRenderer = ${String.toUpperCase(Array.first(item))}
                @process.join = ${Array.join(value, '_')}
            }

            scheduledExport = ${Type.instance(formType.scheduledExport, 'PunktDe\Form\Persistence\Domain\Model\ScheduledExport') ? '<i class="fas fa-envelope icon-white"></i>' : '-'}

            renderer = afx`
                <tr>
                    <td>{formData.siteName}</td>
                    <td>{props.contentDimensions}</td>
                    <td>{formData.formIdentifier}</td>
                    <td>{String.crop(formData.hash,10)}</td>
                    <td>{props.entryCount}</td>
                    <td>{props.date}</td>
                    <td>{props.scheduledExport}</td>
                    <td>
                        <Neos.Fusion.Form:Form form.target.action="download" form.target.controller="Backend\\FormData">
                            <Neos.Fusion.Form:Select field.name="exportDefinitionIdentifier">{props.options}</Neos.Fusion.Form:Select>
                            <Neos.Fusion.Form:Hidden field.name="formIdentifier" attributes.value={formData.formIdentifier}/>
                            <Neos.Fusion.Form:Hidden field.name="hash" attributes.value={formData.hash}/>
                            <Neos.Fusion.Form:Hidden field.name="siteName" attributes.value={formData.siteName}/>
                            <Neos.Fusion.Form:Hidden field.name="dimensionsHash" attributes.value={formData.dimensionsHash}/>
                            <Neos.Fusion.Form:Hidden field.name="hash" attributes.value={formData.hash}/>
                            <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-primary">{I18n.translate('PunktDe.Form.Persistence:Backend.FormData:index.download')}</Neos.Fusion.Form:Button>
                        </Neos.Fusion.Form:Form>
                    </td>
                    <td class="neos-action">
                        <div class="neos-pull-right">
                            <Neos.Fusion.Form:Form attributes.style="display: inline-block" form.target.action="preview" form.target.controller="Backend\\FormData" form.data.formDataEntry={formData}>
                                <Neos.Fusion.Form:Hidden field.name="formDataEntry[hash]"/>
                                <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-primary"><i class="fas fa-eye icon-white"></i></Neos.Fusion.Form:Button>
                            </Neos.Fusion.Form:Form>
                            {props.deleteButton}
                        </div>
                    </td>
                </tr>
            `
        }
    }

    renderer = afx`
        <div class="neos-content neos-container-fluid">
            <div class="neos-module-container">
                <table class="neos-table" @if.hasSavedFormData={props.tableRows}>
                    <thead>
                        <tr>
                            <th>
                                {I18n.translate('PunktDe.Form.Persistence:Backend.FormData:index.site')}
                            </th>
                            <th>
                                {I18n.translate('PunktDe.Form.Persistence:Backend.FormData:index.dimension')}
                            </th>
                            <th>
                                {I18n.translate('PunktDe.Form.Persistence:Backend.FormData:index.form.identifier')}
                            </th>
                            <th>
                                {I18n.translate('PunktDe.Form.Persistence:Backend.FormData:index.version')}
                            </th>
                            <th>
                                {I18n.translate('PunktDe.Form.Persistence:Backend.FormData:index.count')}
                            </th>
                            <th>
                                {I18n.translate('PunktDe.Form.Persistence:Backend.FormData:index.latest')}
                            </th>
                            <th>
                                {I18n.translate('PunktDe.Form.Persistence:Backend.FormData:index.scheduled')}
                            </th>
                            <th>
                                {I18n.translate('PunktDe.Form.Persistence:Backend.FormData:index.export.definition')}
                            </th>
                            <th/>
                        </tr>
                    </thead>
                    <tbody>
                        {props.tableRows}
                    </tbody>
                </table>
                <div @if.hasNoSavedFormData={!props.tableRows}>{I18n.translate('PunktDe.Form.Persistence:Backend.FormData:index.no.saved.form.data')}</div>
            </div>
            <div class="neos-footer">
                <a class="neos-button neos-button-primary" title="Export definitions" href={props.exportDefinitionsLink}>{I18n.translate('PunktDe.Form.Persistence:Backend.FormData:index.manage.export.definitions')}</a>
            </div>
        </div>
    `
}
