prototype(PunktDe.Form.Persistence:Backend.FormData.Index) < prototype(Neos.Fusion:Component) {

     exportDefinitionsLink = Neos.Fusion:UriBuilder {
        package = 'PunktDe.Form.Persistence'
        controller = 'Backend\\ExportDefinition'
        action = 'new'
        additionalParams {
            formIdentifier = ${formType[0].formIdentifier}
            hash = ${formType[0].hash}
        }
    }

    tableRows = Neos.Fusion:Loop {
        items = ${formTypes}
        itemName = 'formType'
        itemRenderer = Neos.Fusion:Component {

            date = ${Date.format(formType[0].date, 'Y-m-d H:i:s')}
            formObject = ${formType[0]}
            entryCount = ${formType.entryCount}

            options = Neos.Fusion:Loop {
                items = ${PunktDe.FormExport.ExportDefinition.getSuitableExportDefinitions(formType[0])}
                itemRenderer = Neos.Fusion.Form:Select.Option {
                    option.value = ${item.identifier}
                    content = ${item.label}
                }
            }

            renderer = afx`
                <tr>
                    <td>{props.formObject.formIdentifier}</td>
                    <td>{String.crop(props.formObject.hash,10)}</td>
                    <td>{props.entryCount}</td>
                    <td>{props.date}</td>
                    <td class="neos-action">
                        <Neos.Fusion.Form:Form form.target.action="download" form.target.controller="Backend\\FormData">
                            <Neos.Fusion.Form:Select field.name="exportDefinitionIdentifier">{props.options}</Neos.Fusion.Form:Select>
                            <Neos.Fusion.Form:Hidden field.name="formIdentifier" attributes.value={props.formObject.formIdentifier}/>
                            <Neos.Fusion.Form:Hidden field.name="hash" attributes.value={props.formObject.hash}/>
                            <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-primary">Download</Neos.Fusion.Form:Button>
                        </Neos.Fusion.Form:Form>

                    </td>
                </tr>
            `
        }
    }

    renderer = afx`
        <div class="neos-content neos-container-fluid">
            <div class="neos-module-container">
                <table class="neos-table">
                    <thead>
                        <tr>
                            <th>
                                Form Identifier
                            </th>
                            <th>
                                Form Version
                            </th>
                            <th>
                                Entry Count
                            </th>
                            <th>
                                Latest Entry
                            </th>
                            <th/>
                        </tr>
                    </thead>
                    <tbody>
                        {props.tableRows}
                    </tbody>
                </table>
            </div>
            <div class="neos-footer">
                <a class="neos-button neos-button-primary" title="Export definitions" href={props.exportDefinitionsLink}>Export definitions</a>
            </div>
        </div>
    `
}
