prototype(PunktDe.Form.Persistence:Backend.FormData.Preview) < prototype(Neos.Fusion:Component) {

    backToFormDataLink = Neos.Fusion:UriBuilder {
        package = 'PunktDe.Form.Persistence'
        controller = 'Backend\\FormData'
        action = 'index'
    }

    headerFields = Neos.Fusion:Loop {
        items = ${Array.unshift(headerFields, 'Date')}
        itemRenderer = ${'<th>' + item + '</th>'}
    }

    tableRows = Neos.Fusion:Loop {
        items = ${formDataEntries}
        itemName = 'fromDataEntry'
        itemRenderer = Neos.Fusion:Fragment {
            formValues = Neos.Fusion:Loop {
                items = ${Array.unshift(fromDataEntry.processedFormData, Date.format(fromDataEntry.date, 'Y-m-d H:i:s'))}
                itemRenderer = Neos.Fusion:Value {
                    nowrap = ${String.length(item) < 40 ? ' nowrap' : ''}
                    value = ${'<td' + this.nowrap + '>' + item + '</td>'}
                }
            }

            deleteButton = PunktDe.Form.Persistence:Component.Atom.DeleteButtonWithConfirmation {
                action = 'deleteSingleFormDataEntry'
                index = ${iterator.index}
                fromObject = ${fromDataEntry}
                fieldName = 'formDataEntry[hash]'
            }

            renderer = afx`
                <tr>
                    {props.formValues}
                    <td>{props.deleteButton}</td>
                </tr>
            `
        }
    }

    scheduledExportInfo = Neos.Fusion:Component {
        renderer = afx`
            <div class="neos-module-container">
                <br />
                <h2>Data of this form is automatically exported and sent via mail to {scheduledExport.email}.</h2>
                <br />
            </div>
        `

        @if.hasScheduledExportDefine = ${Type.instance(scheduledExport, 'PunktDe\Form\Persistence\Domain\Model\ScheduledExport')}
    }

    renderer = afx`
        <legend>{formIdentifier}</legend>
        <div class="neos-content neos-container-fluid">
            {props.scheduledExportInfo}
            <div class="neos-module-container">
                <table class="neos-table">
                    <thead>
                    <tr>{props.headerFields}</tr>
                    </thead>
                    <tbody>
                    {props.tableRows}
                    </tbody>
                </table>
            </div>
            <div class="neos-footer">
                <a class="neos-button neos-button-primary" title="Export definitions" href={props.backToFormDataLink}>Back
                    to Form data export</a>
            </div>
        </div>
    `
}
