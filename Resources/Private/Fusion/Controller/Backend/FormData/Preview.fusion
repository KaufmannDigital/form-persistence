prototype(PunktDe.Form.Persistence:Backend.FormData.Preview) < prototype(Neos.Fusion:Component) {

    backToFormDataLink = Neos.Fusion:UriBuilder {
        package = 'PunktDe.Form.Persistence'
        controller = 'Backend\\FormData'
        action = 'index'
    }

    headerFields = Neos.Fusion:Loop {
        items = ${Array.unshift(headerFields, 'Date')}
        itemRenderer = ${'<th>' + item + '</th>'}
    }

    tableRows = Neos.Fusion:Loop {
        items = ${formDataEntries}
        itemName = 'fromDataEntry'
        itemRenderer = Neos.Fusion:Fragment {
            formValues = Neos.Fusion:Loop {
                items = ${Array.unshift(fromDataEntry.processedFormData, Date.format(fromDataEntry.date, 'Y-m-d H:i:s'))}
                itemRenderer = Neos.Fusion:Value {
                    nowrap = ${String.length(item) < 40 ? ' nowrap' : ''}
                    value = ${'<td' + this.nowrap + '>' + item + '</td>'}
                }
            }

            deleteButton = Neos.Fusion:Fragment {
                renderer = afx`
                    <button class="neos-button neos-button-danger" title={I18n.translate('PunktDe.Form.Persistence:Modules.FormPreview:formdata.delete.tooltip')} data-toggle="modal" href={'#user-' + iterator.index} data-neos-toggle="tooltip">
                        <i class="fas fa-trash-alt icon-white"></i>
                    </button>
                    <div class="neos-hide" id={'user-' + iterator.index}>
                        <div class="neos-modal-centered">
                            <div class="neos-modal-content">
                                <div class="neos-modal-header">
                                    <button type="button" class="neos-close neos-button" data-dismiss="modal"></button>
                                    <div class="neos-header">
                                        {I18n.translate('PunktDe.Form.Persistence:Modules.FormPreview:formdata.delete.modal.title')}
                                    </div>
                                    <div>
                                        <div class="neos-subheader">
                                            <p>
                                                {I18n.translate('PunktDe.Form.Persistence:Modules.FormPreview:formdata.delete.modal.body')}
                                            </p>
                                        </div>
                                    </div>
                                </div>
                                <div class="neos-modal-footer">
                                    <a href="#" class="neos-button" data-dismiss="modal">
                                        {I18n.translate('PunktDe.Form.Persistence:Modules.FormPreview:entry.delete.modal.cancel')}
                                    </a>
                                    <Neos.Fusion.Form:Form attributes.style="display: inline-block" form.target.action="deleteSingleFormDataEntry" form.target.controller="Backend\\FormData" form.data.formDataEntry={fromDataEntry}>
                                        <Neos.Fusion.Form:Hidden field.name="formDataEntry[hash]"/>
                                        <Neos.Fusion.Form:Button type="submit" attributes.class="neos-button neos-button-danger">
                                            {I18n.translate('PunktDe.Form.Persistence:Modules.FormPreview:entry.delete.modal.confirm')}
                                        </Neos.Fusion.Form:Button>
                                    </Neos.Fusion.Form:Form>
                                </div>
                            </div>
                        </div>
                        <div class="neos-modal-backdrop neos-in"></div>
                    </div>
                `
            }

            renderer = afx`
                <tr>
                    {props.formValues}
                    <td>{props.deleteButton}</td>
                </tr>
            `
        }
    }

    scheduledExportInfo = Neos.Fusion:Component {
        renderer = afx`
            <div class="neos-module-container">
                <br />
                <h2>Data of this form is automatically exported and sent via mail to {scheduledExport.email}.</h2>
                <br />
            </div>
        `

        @if.hasScheduledExportDefine = ${Type.instance(scheduledExport, 'PunktDe\Form\Persistence\Domain\Model\ScheduledExport')}
    }

    renderer = afx`
        <legend>{formIdentifier}</legend>
        <div class="neos-content neos-container-fluid">
            {props.scheduledExportInfo}
            <div class="neos-module-container">
                <table class="neos-table">
                    <thead>
                    <tr>{props.headerFields}</tr>
                    </thead>
                    <tbody>
                    {props.tableRows}
                    </tbody>
                </table>
            </div>
            <div class="neos-footer">
                <a class="neos-button neos-button-primary" title="Export definitions" href={props.backToFormDataLink}>Back
                    to Form data export</a>
            </div>
        </div>
    `
}
